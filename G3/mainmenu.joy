(*
    module  : mainmenu.joy
    version : 1.11
    date    : 01/04/26
*)

main_action ==

[[1 reg_init rep_menu [bool] [reg_loop] while]
 [2 set_init rep_menu [bool] [set_loop] while]
 [3 rep_init rep_menu [bool] [rep_loop] while]
[]] case 9 [pop] times main_menu;

main_init ==

(* Start the main menu *)
[MAIN_MENU REGISTER SETTINGS REPORTS]

(* And choices that are allowed *)
['1 '2 '3] [ZLARR ZRARR ZDARR ZUARR] [call swons] step

(* Calculate the x-position of the menu and the title *)
over rest [call size] map qsort last 3 pick - neg 2 /;

(* Repeat the main menu until quit *)
main_loop ==

(* Establish the valid input keys *)
3 pick

(* Read a valid key from input *)
getkey [has not] [pop getkey] while

(*
    If UARR or DARR, whiten the old index, calculate a new one,
    redden the new one, echo the index and continue. Any other
    key causes some other screen to appear.
*)
[[[ZUARR =] menu_old pred [0 =] [pop 3] [] ifte menu_new]
 [[ZDARR =] menu_old succ [4 =] [pop 1] [] ifte menu_new]
 [[ZLARR =] ]
 [[ZRARR =] pop2 dup main_action]
 [popd popd dup 48 - main_action]]
cond;

main_menu ==

rep_menu

(* Print the leave instruction on the bottom line *)
PRESS_TO_LEAVE 1 5 pick false print;
