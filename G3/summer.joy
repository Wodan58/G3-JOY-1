(*
    module  : summer.joy
    version : 1.5
    date    : 02/17/25
*)

summer ==		(* [rek] [jrn] *)
dup size		(* [rek] [jrn] size *)
[unswons		(* [rek] [jrn] [reknr bedr] *)
i			(* [rek] [jrn] reknr bedr *)
swap			(* [rek] [jrn] bedr reknr *)
3 pick			(* [rek] [jrn] bedr reknr [rek] *)
account_find		(* [rek] [jrn] bedr reknr index *)
[negative]
[pop3]			(* [rek] [jrn] *)
[popd			(* [rek] [jrn] bedr index *)
 3 pick			(* [rek] [jrn] bedr index [rek] *)
 size			(* [rek] [jrn] bedr index size *)
 -			(* [rek] [jrn] bedr index *)
 neg			(* [rek] [jrn] bedr index *)
 pred			(* [rek] [jrn] bedr index *)
 swap			(* [rek] [jrn] index bedr *)
 over			(* [rek] [jrn] index bedr index *)
 4 pick			(* [rek] [jrn] index bedr index [rek] *)
 of			(* [rek] [jrn] index bedr [reknr saldo] *)
 dup			(* [rek] [jrn] index bedr [reknr saldo] [reknr saldo] *)
 third			(* [rek] [jrn] index bedr [reknr saldo] saldo *)
 swapd			(* [rek] [jrn] index [reknr saldo] bedr saldo *)
 +			(* [rek] [jrn] index [reknr saldo] new_saldo *)
 [i] dip		(* [rek] [jrn] index reknr omschr saldo new_saldo *)
 popd			(* [rek] [jrn] index reknr omschr new_saldo *)
 []			(* [rek] [jrn] index reknr omschr new_saldo [] *)
 cons cons cons		(* [rek] [jrn] index [reknr saldo] *)
 rolldownd		(* [jrn] index [rek] [reknr saldo] *)
 rollup			(* [jrn] [reknr saldo] index [rek] *)
 swap			(* [jrn] [reknr saldo] [rek] index *)
 over			(* [jrn] [reknr saldo] [rek] index [rek] *)
 over			(* [jrn] [reknr saldo] [rek] index [rek] index *)
 succ			(* [jrn] [reknr saldo] [rek] index [rek] index *)
 drop			(* [jrn] [reknr saldo] [rek] index [rek] *)
 [take] dip		(* [jrn] [reknr saldo] [rek] [rek] *)
 enconcat		(* [jrn] [rek] *)
 swap			(* [rek] [jrn] *)
] ifte ]
times pop;		(* [rek] *)
